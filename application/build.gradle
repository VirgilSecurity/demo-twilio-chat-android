apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply from: '../config-android.gradle'

// App name and version

enum BuildType {
    DEBUG,
    RELEASE
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1

static def getApkName(BuildType buildType) {
    String APP_NAME = "Virgil Messenger "
    switch (buildType) {
        case BuildType.DEBUG:
            return APP_NAME + BuildType.DEBUG.name()
        case BuildType.RELEASE:
            return APP_NAME + BuildType.RELEASE.name()
    }
}

// Android configurations

android {
    defaultConfig {
        applicationId 'com.virgilsecurity.android.virgilmessenger'
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            jniDebuggable false
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
    applicationVariants.all { variant ->

        def applicationName = "Virgil Messenger"
        def apkName

        if (variant.name == android.buildTypes.debug.name) {
            apkName = getApkName(BuildType.DEBUG) + " v" +
                    versionMajor + "." +
                    versionMinor + "." +
                    versionPatch
        } else {
            apkName = getApkName(BuildType.RELEASE) + " v" +
                    versionMajor + "." +
                    versionMinor + "." +
                    versionPatch
        }

        resValue "string", "app_name", applicationName

        variant.outputs.all { output ->
            outputFileName = "${apkName}.apk"
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    productFlavors {
    }
}

// Inner dependencies
dependencies {
    implementation project(':base')
    implementation project(':common')
    implementation project(':feature-login')
    api project(':feature-drawer-navigation')
}

// Outer dependencies
dependencies {
    kotlin()
    support()
    networking()
    reactiveness()
    koin()
    twilio()
    virgil()
    glide()
    architectureComponents()
    firebase()
}

apply plugin: 'com.google.gms.google-services'
